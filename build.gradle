// kts https://paste.helpch.at/udizilomum.kts

import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.0'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'org.cadixdev.licenser' version '0.6.0'
}

group 'me.gabytm.minecraft'
version '1.1.0'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        name 'NBT API'
        url 'https://repo.codemc.org/repository/maven-public/'
    }

    maven {
        name 'JSONMessage'
        url 'https://rayzr.dev/repo/'
    }

    maven {
        name 'Config'
        url 'https://repo.mattstudios.me/artifactory/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib'

    implementation 'me.mattstudios.utils:matt-framework:1.4.6' // Commands - https://mf.mattstudios.me/mf/mf-1
    implementation 'me.mattstudios:triumph-config:1.0.5-SNAPSHOT'

    implementation 'de.tr7zw:item-nbt-api:2.7.1' // https://spigotmc.org/resources/7939/
    implementation 'me.rayzr522:jsonmessage:1.2.1' // https://github.com/Rayzr522/JSONMessage

    // Heads
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.0'

    implementation 'commons-cli:commons-cli:1.4' // https://commons.apache.org/proper/commons-cli/index.html
}

compileJava.options.encoding = "UTF-8"

compileKotlin.kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
    javaParameters = true
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

license {
    header = project.file('headers/header.txt')

    properties {
        year = new Date().format("yyyy")
    }

    include '**/*.kt'
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    archiveName("GUIHelper [${project.version}].jar")

    [
            'kotlin'                  : 'kt', // 'kt' to stop other plugins from loading it from my jar
            'me.mattstudios.config'   : 'defaults',
            'me.mattstudios.mf'       : 'commands',
            'de.tr7zw.changeme.nbtapi': 'nbtapi'
    ].each { relocate(it.key, 'me.gabytm.minecraft.guihelper.libs.' + it.value) }
}

task copyJarToServer(type: Copy) {
    from shadowJar
    into "./server/plugins"
}
